
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
  "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <meta http-equiv="X-UA-Compatible" content="IE=Edge" />
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <title>channelheads.cl &#8212; Streamlines  documentation</title>
    <link rel="stylesheet" href="../_static/alabaster.css" type="text/css" />
    <link rel="stylesheet" href="../_static/pygments.css" type="text/css" />
    <script type="text/javascript" id="documentation_options" data-url_root="../" src="../_static/documentation_options.js"></script>
    <script type="text/javascript" src="../_static/jquery.js"></script>
    <script type="text/javascript" src="../_static/underscore.js"></script>
    <script type="text/javascript" src="../_static/doctools.js"></script>
    <script type="text/javascript" src="../_static/language_data.js"></script>
    <link rel="index" title="Index" href="../genindex.html" />
    <link rel="search" title="Search" href="../search.html" />
    <link rel="next" title="computestep.cl" href="computestep.cl.html" />
    <link rel="prev" title="trajectories.py" href="trajectories.html" />
   
  <link rel="stylesheet" href="../_static/custom.css" type="text/css" />
  
  
  <meta name="viewport" content="width=device-width, initial-scale=0.9, maximum-scale=0.9" />

  </head><body>
  

    <div class="document">
      <div class="documentwrapper">
        <div class="bodywrapper">
          <div class="body" role="main">
            
  <div class="section" id="channelheads-cl">
<h1><code class="docutils literal notranslate"><span class="pre">channelheads.cl</span></code><a class="headerlink" href="#channelheads-cl" title="Permalink to this headline">Â¶</a></h1>
<div class="highlight-default notranslate"><div class="highlight"><pre><span></span>///
/// @file channelheads.cl
///
/// Kernels to map provisional channel heads and the prune those not on thin channels.
///
/// @author CPS
///

///
/// @defgroup structure Connectivity structure
/// Map channel connectivity such as channel heads, confluences, downstream pixels
///

#ifdef KERNEL_MAP_CHANNEL_HEADS
///
/// Map provisional channel heads, even including those not on an IS_THINCHANNEL pixel
///     and thus extraneous. The latter are removed by prune_channel_heads().
///
/// Compiled if KERNEL_MAP_CHANNEL_HEADS is defined.
///
/// @param[in]  seed_point_array: list of initial streamline point vectors,
///                               one allotted to each kernel instance
/// @param[in]  mask_array: grid pixel mask (padded),
///                         with @p true = masked, @p false = good
/// @param[in]  uv_array: flow unit velocity vector grid (padded)
/// @param[in,out] mapping_array: flag grid recording status of each pixel (padded)
///
/// @returns void
///
/// @ingroup structure
///
__kernel void map_channel_heads(
        __global const float2 *seed_point_array,
        __global const bool   *mask_array,
        __global const float2 *uv_array,
        __global       uint   *mapping_array
   )
{
    // For every non-masked pixel, all of which are temporarily flagged as
    //   channel heads, and most of which will be unflagged here

    const uint global_id = get_global_id(0u)+get_global_id(1u)*get_global_size(0u);
#ifdef VERBOSE
    // Report how kernel instances are distributed
    if (global_id==0 || global_id==get_global_offset(0u)) {
        printf(&quot;\n  &gt;&gt;&gt; on GPU/OpenCL device: id=%d offset=%d &quot;,
                get_global_id(0u),
                get_global_offset(0u));
        printf(&quot;#workitems=%d x #workgroups=%d = %d=%d\n&quot;,
                get_local_size(0u), get_num_groups(0u),
                get_local_size(0u)*get_num_groups(0u),
                get_global_size(0u));
    }
#endif
    if (global_id&gt;=N_SEED_POINTS) {
        // This is a &quot;padding&quot; seed, so let&#39;s bail
        return;
    }
    __private uint idx, prev_idx, n_steps = 0u;
    __private float dl = 0.0f, dt = DT_MAX;
    __private float2 uv1_vec, uv2_vec, dxy1_vec, dxy2_vec,
                     vec = seed_point_array[global_id], next_vec;

    // Remember here
    idx = get_array_idx(vec);
//#ifdef DEBUG
//    printf(&quot;Map channel heads (%d) @ %d %g,%g\n&quot;,
//            global_id, idx,vec[0],vec[1]);
//#endif
    prev_idx = idx;
    // Integrate downstream one pixel
    while (prev_idx==idx &amp;&amp; !mask_array[idx] &amp;&amp; n_steps&lt;MAX_N_STEPS) {
        compute_step_vec(dt, uv_array, &amp;dxy1_vec, &amp;dxy2_vec, &amp;uv1_vec, &amp;uv2_vec,
                         vec, &amp;next_vec, &amp;idx);
        channelheads_runge_kutta_step(&amp;dt, &amp;dl, &amp;dxy1_vec, &amp;dxy2_vec,
                                      &amp;vec, &amp;next_vec, &amp;n_steps, &amp;idx);
    }
    if (n_steps&gt;=MAX_N_STEPS) {
        return;
    }
    // Unset the channel head flag unless we&#39;re at a provisional channel head
    if (!mask_array[idx]) {
        idx = get_array_idx(vec);
        if (   ((~mapping_array[idx]) &amp; IS_THINCHANNEL)
            // If here is not channel...
            || (mapping_array[prev_idx] &amp; IS_THINCHANNEL) ) {
            // However, if here is a channel, and if previous pixel was a channel...
            atomic_and(&amp;mapping_array[idx],~IS_CHANNELHEAD);
            // ... flag here as not channel head
        }
    }
    return;
}
#endif

#ifdef KERNEL_PRUNE_CHANNEL_HEADS

// Check if this nbr is a thin channel pixel and not masked
// If so, add one to the &#39;flag&#39;.
// Add 16 if it&#39;s masked, thus recording if *any* nbr is masked.
#define CHECK_IS_THINCHANNEL(idx) ((mapping_array[idx] &amp; IS_THINCHANNEL)!=0)
//#define CHECK_ISNOT_CHANNELHEAD(idx) ((mapping_array[idx] &amp; WAS_CHANNELHEAD)==0)
#define CHECK_IS_MASKED(idx) (mask_array[idx])
#define CHECK_THINCHANNEL(nbr_vec_x,nbr_vec_y) { \
           idx = get_array_idx((float2)(nbr_vec_x,nbr_vec_y)); \
           flag += ( (CHECK_IS_THINCHANNEL(idx) ) \
                     ); \
        }

// check for masked nbr - turned off
//| CHECK_IS_MASKED(idx)*16);

//*CHECK_ISNOT_CHANNELHEAD(idx)

// Check all eight pixel-nbr directions
#define CHECK_E(vec)  CHECK_THINCHANNEL( vec[0]+1.0f, vec[1]      )
#define CHECK_NE(vec) CHECK_THINCHANNEL( vec[0]+1.0f, vec[1]+1.0f )
#define CHECK_N(vec)  CHECK_THINCHANNEL( vec[0]     , vec[1]+1.0f )
#define CHECK_NW(vec) CHECK_THINCHANNEL( vec[0]-1.0f, vec[1]+1.0f )
#define CHECK_W(vec)  CHECK_THINCHANNEL( vec[0]-1.0f, vec[1]      )
#define CHECK_SW(vec) CHECK_THINCHANNEL( vec[0]-1.0f, vec[1]-1.0f )
#define CHECK_S(vec)  CHECK_THINCHANNEL( vec[0]     , vec[1]-1.0f )
#define CHECK_SE(vec) CHECK_THINCHANNEL( vec[0]+1.0f, vec[1]-1.0f )

///
/// Keep only those provisional channel heads that lie on the &#39;thin channel&#39;
///    skeletonized network and have only one such thin channel pixel neighbor.
/// Also exclude any provisional channel head with any masked-pixel neighbors.
///
/// Compiled if KERNEL_PRUNE_CHANNEL_HEADS is defined.
///
/// @param[in]  seed_point_array: list of initial streamline point vectors,
///                               one allotted to each kernel instance
/// @param[in]  mask_array: grid pixel mask (padded),
///                         with @p true = masked, @p false = good
/// @param[in]  uv_array: flow unit velocity vector grid (padded)
/// @param[in,out] mapping_array: flag grid recording status of each pixel (padded)
///
/// @returns void
///
/// @ingroup structure
///
__kernel void prune_channel_heads(
        __global const float2 *seed_point_array,
        __global const bool   *mask_array,
        __global const float2 *uv_array,
        __global       uint   *mapping_array
   )
{
    // For every provisional channel head pixel...

    const uint global_id = get_global_id(0u)+get_global_id(1u)*get_global_size(0u);
    if (global_id&gt;=N_SEED_POINTS) {
        // This is a &quot;padding&quot; seed, so let&#39;s bail
#ifdef DEBUG
        printf(&quot;Bailing @ %d !in [%d-%d]\n&quot;,
                global_id,get_global_offset(0u),N_SEED_POINTS-1);
#endif
        return;
    }
#ifdef VERBOSE
    // Report how kernel instances are distributed
    if (global_id==0 || global_id==get_global_offset(0u)) {
        printf(&quot;\n  &gt;&gt;&gt; on GPU/OpenCL device: id=%d offset=%d &quot;,
                get_global_id(0u),
                get_global_offset(0u));
        printf(&quot;#workitems=%d x #workgroups=%d = %d=%d\n&quot;,
                get_local_size(0u), get_num_groups(0u),
                get_local_size(0u)*get_num_groups(0u),
                get_global_size(0u));
    }
#endif
    __private uint idx;
    __private uint flag=0u;
    const float2 vec = seed_point_array[global_id];
    // Scan all 8 next/nearest neighbors:
    //   - add 1 to flag if the nbr is a thin channel pixel
    //   - add 16 if the nbr is masked (pathological case: 8*16=128)
    CHECK_N(vec);
    CHECK_S(vec);
    CHECK_E(vec);
    CHECK_W(vec);
    CHECK_NE(vec);
    CHECK_SE(vec);
    CHECK_NW(vec);
    CHECK_SW(vec);
    // If flag==1, one and only one nbr is a thin channel pixel
    // Otherwise, remove this provisional channel head.
    if (flag!=1u) {
        idx = get_array_idx(vec);
        atomic_and(&amp;mapping_array[idx],~IS_CHANNELHEAD);
        // If there are no thin channel neighbors AT ALL we must be at an isolated pixel.
        // Thus redesignate this pixel as &#39;not channelized at all&#39;.
        if (flag==0) { // || flag&gt;=16) {
            atomic_and(&amp;mapping_array[idx], ~(IS_THINCHANNEL | IS_CHANNEL));
        }
    }
    return;
}
#endif
</pre></div>
</div>
</div>


          </div>
        </div>
      </div>
      <div class="sphinxsidebar" role="navigation" aria-label="main navigation">
        <div class="sphinxsidebarwrapper">
<p class="logo">
  <a href="../index.html">
    <img class="logo" src="../_static/../_images/icon3.png" alt="Logo"/>
    
    <h1 class="logo logo-name">Streamlines</h1>
    
  </a>
</p>



<p class="blurb">Topographic streamline mapping of landscape structure</p>







<div id="searchbox" style="display: none" role="search">
  <h3>Quick search</h3>
    <div class="searchformwrapper">
    <form class="search" action="../search.html" method="get">
      <input type="text" name="q" />
      <input type="submit" value="Go" />
      <input type="hidden" name="check_keywords" value="yes" />
      <input type="hidden" name="area" value="default" />
    </form>
    </div>
</div>
<script type="text/javascript">$('#searchbox').show(0);</script><div class="relations">
<h3>Related</h3>
<ul>
  <li><a href="../index.html"></a><ul>
      <li><a href="trajectories.html" title="previous chapter"><code class="docutils literal notranslate"><span class="pre">trajectories.py</span></code></a></li>
      <li><a href="computestep.cl.html" title="next chapter"><code class="docutils literal notranslate"><span class="pre">computestep.cl</span></code></a></li>
  </ul></li>
</ul>
</div>
        </div>
      </div>
      <div class="clearer"></div>
    </div>
    <div class="footer">
      &copy;2018, CPS.
      
      |
      Powered by <a href="http://sphinx-doc.org/">Sphinx 1.8.3</a>
      &amp; <a href="https://github.com/bitprophet/alabaster">Alabaster 0.7.12</a>
      
      |
      <a href="../_sources/modules/channelheads.cl.rst.txt"
          rel="nofollow">Page source</a>
    </div>

    

    
  </body>
</html>